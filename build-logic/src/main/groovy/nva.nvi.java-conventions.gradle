plugins {
    id 'nva.nvi.common-conventions'
    id 'java-library'
    id 'jacoco'
    id 'pmd'
    id 'net.ltgt.errorprone'
}

group = 'com.github.bibsysdev'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        // Suppress build failures from lint errors (remove this if possible)
        allErrorsAsWarnings = true
    }
}

pmd {
    toolVersion = '7.11.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

tasks.named('test', Test) {
    useJUnitPlatform()
    failFast = false
    testLogging {
        events = ['skipped', 'passed', 'failed']
        showCauses = true
        exceptionFormat = "full"
    }
}

// Do not generate test reports for individual projects
tasks.named("jacocoTestReport", JacocoReport) {
    enabled = false
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named('check') {
    dependsOn tasks.named('test')
}
