plugins {
    id 'base'
    id 'java'
    id 'com.diffplug.spotless'
}

// Configure project-wide automatic formatting
spotless {
    java {
        toggleOffOn() // Ignores sections between `spotless:off` / `spotless:on`
        googleJavaFormat().reflowLongStrings().formatJavadoc(true).reorderImports(true)
    }

    groovyGradle {
        target('**/*.gradle')
        greclipse()
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'misc', {
        target('.gitignore', '.gitattributes', '.editorconfig')
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'markdown', {
        target('**/*.md')
        prettier().config(['proseWrap': 'preserve'])
        endWithNewline()
    }

    format 'yaml', {
        target('**/*.yaml', '**/*.yml')
        prettier().config(['printWidth': 120])
        endWithNewline()
    }
}

// Reformat project before build
tasks.named('build').configure {
    dependsOn('spotlessApply')
}

tasks.named('test').configure {
    dependsOn('spotlessApply')
}
