plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco-report-aggregation'
    id 'com.diffplug.spotless'
}

group = 'com.github.bibsysdev'

dependencies {
    testImplementation libs.bundles.testing

    //To support dynamodb local when testing, using mac m1
    if (System.getProperty("os.arch") == "aarch64") {
        testImplementation group: 'io.github.ganadist.sqlite4java', name: 'libsqlite4java-osx-aarch64', version: '1.0.392'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21 // source-code version and must be <= targetCompatibility
    targetCompatibility = JavaVersion.VERSION_21 // bytecode target version
}

tasks.named('test') {
    useJUnitPlatform()
    failFast = false
    testLogging {
        events 'skipped', 'passed', 'failed'
        showCauses true
        exceptionFormat "full"
    }
}

pmd {
    toolVersion = '7.8.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {

    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }
    }
}

spotless {
    java {
        toggleOffOn() // Ignores sections between `spotless:off` / `spotless:on`
        googleJavaFormat().reflowLongStrings().formatJavadoc(true).reorderImports(true)
    }

    format 'misc', {
        target '.gitignore', '.gitattributes', '.editorconfig', '**/*.gradle'
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}
