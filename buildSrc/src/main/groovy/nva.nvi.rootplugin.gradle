plugins {
    id 'jacoco-report-aggregation'
    id 'base'
    id 'java-library'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://s3.eu-central-1.amazonaws.com/dynamodb-local-frankfurt/release" }
}

dependencies {
    subprojects.forEach {
        jacocoAggregation it
    }
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
        integrationTestCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.INTEGRATION_TEST
        }
    }
}

// Log a clickable link to console for generated test coverage reports
def reportDirPath = "reports/jacoco/testCodeCoverageReport/html"
def reportDir = layout.buildDirectory.dir(reportDirPath).get().asFile
subprojects {
    afterEvaluate {
        tasks.named('testCodeCoverageReport').configure {
            doLast {
                println "Test coverage report for ${project.name}: file://${reportDir}/index.html"
            }
        }
    }
}

tasks.named('testCodeCoverageReport').configure {
    doLast {
        println "Combined test coverage report: file://${reportDir}/index.html"
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
    dependsOn tasks.named('integrationTestCodeCoverageReport', JacocoReport)
}
